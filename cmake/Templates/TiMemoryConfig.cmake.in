
# cmake_policy(SET CMP0057 NEW)

include(${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@ConfigVersion.cmake)

@PACKAGE_INIT@

#------------------------------------------------------------------------------#
#   Settings
#------------------------------------------------------------------------------#
set(TIMEMORY_PYTHON_BINDINGS @TIMEMORY_BUILD_PYTHON@)
if(NOT DEFINED @PROJECT_NAME@_DIR)
    set(@PROJECT_NAME@_DIR ${CMAKE_CURRENT_LIST_DIR})
endif(NOT DEFINED @PROJECT_NAME@_DIR)
set(TIMEMORY_C_LIBRARY @TIMEMORY_BUILD_C@)
set(TIMEMORY_CUDA_LIBRARY OFF)
if(@TIMEMORY_USE_CUPTI@ AND @TIMEMORY_USE_CUDA@)
    set(TIMEMORY_CUDA_LIBRARY ON)
endif()

# compile options
set(@PROJECT_NAME@_C_COMPILE_OPTIONS @PROJECT_C_COMPILE_OPTIONS@)
set(@PROJECT_NAME@_CXX_COMPILE_OPTIONS @PROJECT_CXX_COMPILE_OPTIONS@)
set(@PROJECT_NAME@_CUDA_COMPILE_OPTIONS @PROJECT_CUDA_COMPILE_OPTIONS@)
set(@PROJECT_NAME@_BUILD_TYPE @CMAKE_BUILD_TYPE@)

# installed and interface libraries
set(@PROJECT_NAME@_COMPILED_LIBRARIES @TIMEMORY_COMPILED_LIBRARIES@)
set(@PROJECT_NAME@_INTERFACE_LIBRARIES @TIMEMORY_INTERFACE_LIBRARIES@)
set(@PROJECT_NAME@_EXTERNAL_LIBRARIES @TIMEMORY_EXTERNAL_LIBRARIES@)

# language standards
set(CMAKE_C_STANDARD @CMAKE_C_STANDARD@ CACHE STRING "")
set(CMAKE_CXX_STANDARD @CMAKE_CXX_STANDARD@ CACHE STRING "")
set(CMAKE_CUDA_STANDARD @CMAKE_CUDA_STANDARD@ CACHE STRING "")

# language standard required
set(CMAKE_C_STANDARD_REQUIRED @CMAKE_C_STANDARD_REQUIRED@ CACHE BOOL "")
set(CMAKE_CXX_STANDARD_REQUIRED @CMAKE_CXX_STANDARD_REQUIRED@ CACHE BOOL "")
set(CMAKE_CUDA_STANDARD_REQUIRED @CMAKE_CUDA_STANDARD_REQUIRED@ CACHE BOOL "")

# language extensions
set(CMAKE_C_EXTENSIONS @CMAKE_C_EXTENSIONS@ CACHE BOOL "")
set(CMAKE_CXX_EXTENSIONS @CMAKE_CXX_EXTENSIONS@ CACHE BOOL "")
set(CMAKE_CUDA_EXTENSIONS @CMAKE_CUDA_EXTENSIONS@ CACHE BOOL "")

# languages
set(TIMEMORY_LANGUAGES CXX)

if(TIMEMORY_C_LIBRARY)
    list(APPEND TIMEMORY_LANGUAGES C)
endif()

if(TIMEMORY_CUDA_LIBRARY)
    list(APPEND TIMEMORY_LANGUAGES CUDA)
endif()

#------------------------------------------------------------------------------#
#   Determine install tree or build tree
#------------------------------------------------------------------------------#
set(TIMEMORY_INSTALL_TREE ON)
if("${CMAKE_SOURCE_DIR}" STREQUAL "@CMAKE_SOURCE_DIR@")
    set(TIMEMORY_INSTALL_TREE OFF) # build tree
endif()

if(TIMEMORY_INSTALL_TREE)

    set_and_check(@PROJECT_NAME@_INCLUDE_DIR "@PACKAGE_INCLUDE_INSTALL_DIR@")
    set_and_check(@PROJECT_NAME@_LIB_DIR "@PACKAGE_LIB_INSTALL_DIR@")
    get_filename_component(@PROJECT_NAME@_ROOT_DIR ${@PROJECT_NAME@_INCLUDE_DIR} PATH)

    if(@TIMEMORY_BUILD_PYTHON@)
        set(@PROJECT_NAME@_PYTHON_DIR "@PACKAGE_PYTHON_INSTALL_DIR@")
        if(NOT EXISTS ${@PROJECT_NAME@_PYTHON_DIR})
            find_package(PythonInterp QUIET)
            if(PYTHONINTERP_FOUND)
                execute_process(COMMAND
                    ${PYTHON_EXECUTABLE} -c "import site; print(site.getsitepackages()[0])"
                    OUTPUT_VARIABLE @PROJECT_NAME@_PYTHON_DIR
                    OUTPUT_STRIP_TRAILING_WHITESPACE
                    WORKING_DIRECTORY ${CMAKE_CURRENT_LIST_DIR})
            endif()
        endif()
    endif()

    foreach(_TYPE LIB INCLUDE PYTHON)
        set(@PROJECT_NAME@_${_TYPE}_DIRS ${@PROJECT_NAME@_${_TYPE}_DIR})
    endforeach()

    include(${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@LibraryDepends.cmake)
    check_required_components(@PROJECT_NAME@)

else() # build tree

    set(@PROJECT_NAME@_ROOT_DIR "@PROJECT_BINARY_DIR@")

    set(@PROJECT_NAME@_LIB_DIR "@PROJECT_BINARY_DIR@")

    if(@TIMEMORY_BUILD_PYTHON@)
        set(@PROJECT_NAME@_PYTHON_DIR "@PROJECT_BINARY_DIR@")
    endif()

    MACRO(IMPORT_LIB _LIB _IMPORT_TYPE)
        set_property(TARGET ${_LIB} PROPERTY ${_IMPORT_TYPE}
            "${CMAKE_BINARY_DIR}/TiMemoryBuild.cmake")
    ENDMACRO()

    foreach(_LIB ${@PROJECT_NAME@_INTERFACE_LIBRARIES})
        import_lib(${_LIB} INTERFACE_IMPORTED_LOCATION)
    endforeach()

    foreach(_LIB ${@PROJECT_NAME@_COMPILED_LIBRARIES})
        import_lib(${_LIB} IMPORTED_LOCATION)
    endforeach()

    if(WIN32)
        target_compile_definitions(@LIBNAME@-cxx-static PUBLIC _TIMEMORY_ARCHIVE)
        if(TIMEMORY_C_LIBRARY)
            target_compile_definitions(@LIBNAME@-c-static PUBLIC _TIMEMORY_ARCHIVE)
        endif()
    endif()

    if(TIMEMORY_CUDA_LIBRARY)
        set_property(TARGET @LIBNAME@-cupti-shared
            PROPERTY IMPORTED_LOCATION "${CMAKE_BINARY_DIR}/TiMemoryBuild.cmake")
        set_property(TARGET @LIBNAME@-cupti-static
            PROPERTY IMPORTED_LOCATION "${CMAKE_BINARY_DIR}/TiMemoryBuild.cmake")
    endif()

endif()


#------------------------------------------------------------------------------#
#   TiMemory link target
#------------------------------------------------------------------------------#
set(@PROJECT_NAME@_CXX_LIBRARIES @LIBNAME@-cxx)
set(@PROJECT_NAME@_LIBRARIES @LIBNAME@-cxx)
if(TIMEMORY_C_LIBRARY)
    set(@PROJECT_NAME@_C_LIBRARIES @LIBNAME@-c)
    list(APPEND @PROJECT_NAME@_LIBRARIES @LIBNAME@-c)
endif()


#------------------------------------------------------------------------------#
#   timemory component interface library
#------------------------------------------------------------------------------#
set(_@PROJECT_NAME@_MISSING_COMPONENTS )
set(@PROJECT_NAME@_FOUND_COMPONENTS ${@PROJECT_NAME@_FIND_COMPONENTS})

set(_INTERFACE_LIB @LIBNAME@)
if(@PROJECT_NAME@_FIND_COMPONENTS_INTERFACE)
    set(_INTERFACE_LIB ${@PROJECT_NAME@_FIND_COMPONENTS_INTERFACE})
endif()

if(CMAKE_VERSION VERSION_LESS 3.11)
    if(NOT TARGET ${_INTERFACE_LIB})
        add_library(${_INTERFACE_LIB} INTERFACE)
    else()
        set(_MSG "CMake 3.10 does not fully support IMPORTED INTERFACE libraries")
        set(_MSG "${_MSG}. The \"${_INTERFACE_LIB}\" target is being modified globally...")
        set(_MSG "${_MSG}... Consider upgrading CMake or setting")
        set(_MSG "${_MSG} \"@PROJECT_NAME@_FIND_COMPONENTS_INTERFACE\".")
        message(WARNING "${_MSG}")
    endif()
else()
    add_library(${_INTERFACE_LIB} INTERFACE IMPORTED)
endif()

if(NOT "${@PROJECT_NAME@_FIND_COMPONENTS}" STREQUAL "")

    set(_LINK )
    set(_LANGUAGES )
    set(_COMPONENT_LIST )

    # always add headers
    list(APPEND @PROJECT_NAME@_FIND_COMPONENTS headers)

    # determine link type
    if("shared" IN_LIST @PROJECT_NAME@_FIND_COMPONENTS)
        set(_LINK shared)
        list(REMOVE_ITEM @PROJECT_NAME@_FIND_COMPONENTS shared)
        list(REMOVE_ITEM @PROJECT_NAME@_FOUND_COMPONENTS shared)
    elseif("static" IN_LIST @PROJECT_NAME@_FIND_COMPONENTS)
        set(_LINK static)
        list(REMOVE_ITEM @PROJECT_NAME@_FIND_COMPONENTS static)
        list(REMOVE_ITEM @PROJECT_NAME@_FOUND_COMPONENTS static)
    else()
        if(BUILD_SHARED_LIBS)
            set(_LINK shared)
        else()
            set(_LINK static)
        endif()
    endif()

    # find languages
    foreach(_LANGUAGE c cxx cuda)
        if("${_LANGUAGE}" IN_LIST @PROJECT_NAME@_FIND_COMPONENTS)
            list(APPEND _LANGUAGES ${_LANGUAGE})
            list(REMOVE_ITEM @PROJECT_NAME@_FIND_COMPONENTS ${_LANGUAGE})
            list(REMOVE_ITEM @PROJECT_NAME@_FOUND_COMPONENTS ${_LANGUAGE})
        endif()
    endforeach()

    # helper macro
    macro(@PROJECT_NAME@_LINK_TARGET_IF_EXISTS _TARGET)
        if("${_TARGET}" IN_LIST @PROJECT_NAME@_FIND_COMPONENTS OR TARGET ${_TARGET})
            if(TARGET ${_TARGET})
                target_link_libraries(${_INTERFACE_LIB} INTERFACE ${_TARGET})
                list(APPEND @PROJECT_NAME@_FOUND_COMPONENTS ${_TARGET})
            else()
                list(REMOVE_ITEM @PROJECT_NAME@_FOUND_COMPONENTS ${_TARGET})
                list(APPEND _@PROJECT_NAME@_MISSING_COMPONENTS ${_TARGET})
            endif()

            if("${_TARGET}" IN_LIST @PROJECT_NAME@_FIND_COMPONENTS)
                list(REMOVE_ITEM @PROJECT_NAME@_FIND_COMPONENTS ${_TARGET})
            endif()

            foreach(_ARG ${ARGN})
                if("${_ARG}" IN_LIST @PROJECT_NAME@_FIND_COMPONENTS)
                    list(REMOVE_ITEM @PROJECT_NAME@_FIND_COMPONENTS ${_ARG})
                endif()
                list(REMOVE_ITEM @PROJECT_NAME@_FOUND_COMPONENTS ${_ARG})
            endforeach()
        else()
            message(WARNING "Target ${_TARGET} not found!")
            list(REMOVE_ITEM @PROJECT_NAME@_FOUND_COMPONENTS ${_TARGET})
            list(APPEND _@PROJECT_NAME@_MISSING_COMPONENTS ${_TARGET})
        endif()
    endmacro()

    if("extern-templates" IN_LIST @PROJECT_NAME@_FIND_COMPONENTS)
        if(NOT WIN32)
            @PROJECT_NAME@_LINK_TARGET_IF_EXISTS(timemory-extern-templates extern-templates)
        else()
            message(WARNING "timemory-extern-templates not available on Windows")
            list(REMOVE_ITEM @PROJECT_NAME@_FIND_COMPONENTS timemory-extern-templates)
            list(REMOVE_ITEM @PROJECT_NAME@_FIND_COMPONENTS extern-templates)
            list(REMOVE_ITEM @PROJECT_NAME@_FOUND_COMPONENTS timemory-extern-templates)
            list(REMOVE_ITEM @PROJECT_NAME@_FOUND_COMPONENTS extern-templates)
        endif()
    endif()

    foreach(_LANGUAGE ${_LANGUAGES})
        if("${_LANGUAGE}" STREQUAL "cuda")
            @PROJECT_NAME@_LINK_TARGET_IF_EXISTS(timemory-${_LANGUAGE})
        else()
            @PROJECT_NAME@_LINK_TARGET_IF_EXISTS(timemory-${_LANGUAGE}-${_LINK})
            @PROJECT_NAME@_LINK_TARGET_IF_EXISTS(timemory-${_LANGUAGE}-compile-options compile-options)
        endif()
    endforeach()

    if("cupti" IN_LIST @PROJECT_NAME@_FIND_COMPONENTS)
        @PROJECT_NAME@_LINK_TARGET_IF_EXISTS(timemory-cupti-${_LINK} cupti)
    endif()

    foreach(_COMPONENT ${@PROJECT_NAME@_FIND_COMPONENTS})
        @PROJECT_NAME@_LINK_TARGET_IF_EXISTS(timemory-${_COMPONENT} ${_COMPONENT})
    endforeach()

    if(NOT TIMEMORY_CUDA_LIBRARY)
        list(REMOVE_ITEM @PROJECT_NAME@_FOUND_COMPONENTS timemory-cupti-static)
        list(REMOVE_ITEM @PROJECT_NAME@_FOUND_COMPONENTS timemory-cupti-shared)
    endif()

    if(NOT "${@PROJECT_NAME@_FOUND_COMPONENTS}" STREQUAL "")
        list(REMOVE_DUPLICATES @PROJECT_NAME@_FOUND_COMPONENTS)
    endif()
    list(SORT @PROJECT_NAME@_FOUND_COMPONENTS)
    set(_COMPONENT_LIST ${@PROJECT_NAME@_FOUND_COMPONENTS})
    string(REPLACE ";" ", " _COMPONENT_LIST "${_COMPONENT_LIST}")
    string(REPLACE "timemory-" "" _COMPONENT_LIST "${_COMPONENT_LIST}")
    message(STATUS "[${PROJECT_NAME}] @PROJECT_NAME@ : \"${_INTERFACE_LIB}\" components: ${_COMPONENT_LIST}")

    unset(_COMPONENT_LIST)
    unset(_LINK)
    unset(_LANGUAGES)
endif()


#------------------------------------------------------------------------------#
#   find_package variables
#------------------------------------------------------------------------------#
include(FindPackageHandleStandardArgs)
# handle the QUIETLY and REQUIRED arguments and set TiMemory_FOUND to TRUE
# if all listed variables are TRUE
find_package_handle_standard_args(@PROJECT_NAME@ DEFAULT_MSG
    @PROJECT_NAME@_ROOT_DIR
    ${_@PROJECT_NAME@_MISSING_COMPONENTS}
    @PROJECT_NAME@_LIBRARIES)

if(APPLE)
    set(CMAKE_INCLUDE_SYSTEM_FLAG_${LANG} "-isystem ")
endif(APPLE)

unset(TIMEMORY_INSTALL_TREE)
